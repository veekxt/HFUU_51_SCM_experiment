;
;使用外部中断0计数
;jp69按键k1应该接p3.2口
;
DS595 EQU P2.2			;595串行输入
SH EQU P2.1				;595移位寄存器
ST EQU P2.0				;595存储寄存器
SECOND EQU 30H			;变量：秒
MINUTE EQU 31H			;变量：分
HOUR   EQU 32H			;变量：时
K      EQU 33H			;变量:按键次数

ORG 0000H
LJMP MAIN

ORG 000BH				;定时器0中断入口
LJMP T0J
				
ORG 0003H				;外部中断0入口
LJMP KEY_SUM
ORG 0100H

;-----初始化中断&定时器-----;
MAIN:
MOV SECOND,#0
MOV TMOD,#01H
MOV TL0,#176
MOV TH0,#60
SETB IT0				 ;跳沿触发
SETB EX0
SETB ET0
SETB EA
mov R7,#0				 ;使用r7统计定时中断次数
SETB TR0

;-----数码管动态显示---------;
LED:
;第五位秒
LCALL CLR_595			 ;清屏
MOV DPTR,#WEI			 ;送位选（5）
MOV A,#5
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN			 ;送段选（秒的个位）
MOV A,SECOND
MOV B,#10
DIV AB					 ;通过A%10取出秒的个位
MOV A,B
MOVC A,@A+DPTR
LCALL W595
LCALL O595				 ;595并行输出，数码管显示

;第四位秒
LCALL CLR_595
MOV DPTR,#WEI
MOV A,#4
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN
MOV A,SECOND
MOV B,#10
DIV AB					 ;通过A/10取出秒的十位
MOVC A,@A+DPTR
LCALL W595
LCALL O595		  

;第3位分
LCALL CLR_595
MOV DPTR,#WEI
MOV A,#3
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN
MOV A,MINUTE
MOV B,#10
DIV AB
MOV A,B

MOVC A,@A+DPTR
LCALL W595
LCALL O595

;第2位分
LCALL CLR_595
MOV DPTR,#WEI
MOV A,#2
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN
MOV A,MINUTE
MOV B,#10
DIV AB
MOVC A,@A+DPTR
LCALL W595
LCALL O595

;第1位时
LCALL CLR_595
MOV DPTR,#WEI
MOV A,#1
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN
MOV A,HOUR
MOV B,#10
DIV AB
MOV A,B

MOVC A,@A+DPTR
LCALL W595
LCALL O595

;第0位时
LCALL CLR_595
MOV DPTR,#WEI
MOV A,#0
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN
MOV A,HOUR
MOV B,#10
DIV AB
MOVC A,@A+DPTR
LCALL W595
LCALL O595

;第七位按键计数
LCALL CLR_595
MOV DPTR,#WEI
MOV A,#7
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN
MOV A,K
MOV B,#10
DIV AB
MOV A,B
MOVC A,@A+DPTR
LCALL W595
LCALL O595

;第六位按键计数
LCALL CLR_595
MOV DPTR,#WEI
MOV A,#6
MOVC A,@A+DPTR
LCALL W595
MOV DPTR,#DUAN
MOV A,K
MOV B,#10
DIV AB
MOVC A,@A+DPTR
LCALL W595
LCALL O595

CJNE R7,#20,j			 ;R7==20则过了1秒
INC SECOND				 ;秒加一
MOV R7,#0				 ;然后R7清零重新计时

MOV A,SECOND			 ;如果秒到达60则
CJNE A,#60,GOM
MOV SECOND,#0
INC MINUTE				 ;分钟加一
GOM:
MOV A,MINUTE
CJNE A,#60,GOH
MOV MINUTE,#0
INC HOUR
GOH:
MOV A,HOUR
CJNE A,#24,j
MOV HOUR,#0

j: 
LJMP LED

;-----定时器0中断函数-----;
T0J:				   
INC R7
MOV TL0,#176
MOV TH0,#60
RETI
;----外部中断0中断函数-----;
KEY_SUM:
LCALL DELAY12MS			    ;延时12ms （防抖）
JB INT0,GORETI
INC K

MOV A,K						;k到达100则清零
CJNE A,#100,GORETI
MOV K,#0

GORETI:  RETI

;-----写595-----;
W595:
	MOV R5,#8
L1:	
	RLC A
	MOV DS595,C
	CLR SH
	NOP
	SETB SH
	NOP	
	DJNZ R5,L1
RET
;-----595并行输出-----;
O595:
	CLR ST
	NOP
	SETB ST
RET
;-----清屏函数-----;
CLR_595:
	MOV A,#0FFH
	LCALL W595
	MOV A,#0FFH
	LCALL W595
	LCALL O595
RET

;-----12ms的延时程序------（非精确）;
DELAY12MS:
MOV R5,#48
DEL:mov R6,#250
DJNZ R6,$
DJNZ R5,DEL
RET

DUAN:		;段码					  
	DB 0xc0,0xf9,0xa4,0xb0,0x99,0x92,0x82,0xf8,0x80,0x90,0x88,0x83,0xc6,0xa1,0x86,0x8e,0xbf
WEI:		;位码
	DB 0x01,0x02,0x04,0x08,0x10,0x20,0x40,0x80
END	